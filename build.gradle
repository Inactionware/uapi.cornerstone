/*
 * Copyright (C) 2017. The UAPI Authors
 * You may not use this file except in compliance with the License.
 * You may obtain a copy of the License at the LICENSE file.
 *
 * You must gained the permission from the authors if you want to
 * use the project into a commercial product
 */

ext {
    thisRepo = 'cornerstone'
}

apply from: '.config/uapi/all.config.gradle'
apply from: '.config/uapi/base.config.gradle'
apply from: '.config/uapi/cornerstone.config.gradle'
apply from: '.config/uapi/functions.gradle'

apply plugin: 'jacoco'

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    group = "${allCfg.group}"
    version = getBuildVersion(project.name)
    sourceCompatibility = 11
    targetCompatibility = 11

    configurations {
        codacy
    }

    dependencies {
        testCompile coordinate(libs.spock)

        testRuntime (
                coordinate(libs.cglib),      // allows mocking of classes (in addition to interfaces)
                coordinate(libs.objenesis)   // allows mocking of classes without default constructor (together with CGLIB)
        )
    }

    jar {
        manifest.attributes
        exclude('**/*.java')
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    def generatedSources = ['**/*_Generated.*']

    test {
        jacoco {
            excludes = generatedSources
        }
    }

    jacoco {
        toolVersion = libs.jacoco.version
    }

    build.dependsOn jacocoTestReport
}

def publishedProjects = subprojects.findAll { it.path != ':uapi.example' }

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn(publishedProjects.test)

    getAdditionalSourceDirs().from(files(publishedProjects.sourceSets.main.allSource.srcDirs))
    getSourceDirectories().from(publishedProjects.sourceSets.main.allSource.srcDirs)
    getClassDirectories().from(publishedProjects.sourceSets.main.output)
    getExecutionData().from(files(subprojects.jacocoTestReport.executionData).filter { f -> f.exists() })

    reports {
        html.enabled = true // human readable
        xml.enabled = true // required by coveralls
    }
}

configurations {
    codacy
}
